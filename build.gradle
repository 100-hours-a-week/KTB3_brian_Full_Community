plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'

    id 'info.solidsoft.pitest' version '1.15.0'
    id 'jacoco'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
    // 실행 환경
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
    implementation 'org.springframework.boot:spring-boot-starter-security'


    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    // 테스트 환경
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testCompileOnly("org.projectlombok:lombok")

    testAnnotationProcessor("org.projectlombok:lombok")
}

test {
	useJUnitPlatform()
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

def pitestLauncher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(21)
}.get()

pitest {
    pitestVersion = '1.22.0'
    junit5PluginVersion = '1.2.3'

    targetClasses = ['com.community.domain.user.*', 'com.community.domain.auth.*', 'com.community.domain.board.*']

    mutators = ['STRONGER']

    reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
    outputFormats = ['HTML', 'XML']
    threads = 4
    timestampedReports = false

    // ★ 여기서 PIT 이 사용할 java 를 21로 지정
    jvmPath = pitestLauncher.executablePath.asFile.absolutePath
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'PACKAGE'
            includes = ['com.community.domain.*.service']

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }

        rule {
            element = 'PACKAGE'
            includes = ['com.community.domain.*.repository']

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)     // SonarQube 등 연동용
        csv.required.set(false)
        html.required.set(true)    // 우리가 보기 좋은 HTML 리포트
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco").get())
    }

    classDirectories.setFrom(
        files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/config/**',
                    '**/dto/**',
                    '**/model/**',
                    '**/*Application*'    // main 앱 클래스
            ])
        })
    )
}
